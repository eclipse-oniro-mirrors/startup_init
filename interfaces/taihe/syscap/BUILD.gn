# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//base/startup/init/begetd.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

copy_taihe_idl("copy_global") {
  sources = [ "idl/internal.full.global.taihe" ] 
}
subsystem_name = "startup"
part_name = "init"
taihe_generated_file_path_global = "$taihe_file_path/out/$subsystem_name/$part_name/global"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path_global"
  deps = [ ":copy_global" ]
  outputs = [
    "$taihe_generated_file_path/src/internal.full.global.ani.cpp",
    "$taihe_generated_file_path/src/internal.full.global.abi.c",
  ]
}

taihe_shared_library("global_taihe_native") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "$taihe_generated_file_path_global"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "${init_innerkits_path}/include" ]
  sources += [
    "src/ani_constructor.cpp",
    "src/internal.full.global.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${init_innerkits_path}:libbegetutil",
  ]
}

generate_static_abc("global") {
  base_url = "$taihe_generated_file_path_global"
  files = [ "$taihe_generated_file_path_global/@internal.full.global.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/global.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("global_etc") {
  source = "$target_out_dir/global.abc"           
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":global" ]
}
