# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/startup/init/begetd.gni")
import("//build/ohos.gni")

comm_include = [
  ".",
  "..",
  "../../init/include",
  "//base/startup/init/interfaces/innerkits/include/param",
  "//base/startup/init/interfaces/innerkits/include",
]

ohos_shared_library("init_context") {
  sources = [ "init_context.c" ]

  include_dirs = comm_include

  include_dirs += [ "../../init/include" ]

  external_deps = []

  defines = [ "INIT_SUPPORT_CHIPSET_INIT" ]
  if (build_selinux) {
    external_deps += [
      "selinux_adapter:libload_policy",
      "selinux_adapter:librestorecon",
    ]
    defines += [ "WITH_SELINUX" ]
  }

  deps = [ "//base/startup/init/interfaces/innerkits/init_module_engine:libinit_module_engine" ]

  external_deps += [
    "bounds_checking_function:libsec_shared",
    "selinux:libselinux",
  ]

  part_name = "init"
  subsystem_name = "startup"
  if (target_cpu == "arm64" || target_cpu == "x86_64" ||
      target_cpu == "riscv64") {
    module_install_dir = "lib64/init"
  } else {
    module_install_dir = "lib/init"
  }
}

config("initcontext_static_config") {
  include_dirs = comm_include
}

ohos_source_set("initcontext_static") {
  sources = [ "init_context_static.c" ]
  include_dirs = comm_include
  defines = [ "INIT_SUPPORT_CHIPSET_INIT" ]
  public_configs = [ ":initcontext_static_config" ]
  public_configs += [ "../../../interfaces/innerkits/init_module_engine:init_module_engine_exported_config" ]
  external_deps = [ "bounds_checking_function:libsec_shared" ]
  part_name = "init"
  subsystem_name = "startup"
}
